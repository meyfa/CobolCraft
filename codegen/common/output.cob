*> --- Codegen-SetOutputDirectory ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-SetOutputDirectory.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-OUT-DIR              PIC X(255)                  EXTERNAL.
LINKAGE SECTION.
    01 LK-OUT-DIR                   PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-OUT-DIR.
    MOVE LK-OUT-DIR TO CODEGEN-OUT-DIR
    GOBACK.

END PROGRAM Codegen-SetOutputDirectory.

*> --- Codegen-Start ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-Start.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-OUT-DIR              PIC X(255)                  EXTERNAL.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.
    01 CODEGEN-START-TIME           BINARY-LONG-LONG            EXTERNAL.
LINKAGE SECTION.
    01 LK-FILENAME                  PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-FILENAME.
    INITIALIZE CODEGEN-FILE-NAME
    STRING FUNCTION TRIM(CODEGEN-OUT-DIR) "/" FUNCTION TRIM(LK-FILENAME) INTO CODEGEN-FILE-NAME

    CALL "CBL_CREATE_FILE" USING CODEGEN-FILE-NAME 2 0 0 CODEGEN-FILE-HANDLE
    COPY ASSERT REPLACING COND BY ==RETURN-CODE = 0==,
        MSG BY =="Codegen: Failed to open file: " FUNCTION TRIM(CODEGEN-FILE-NAME)==.

    MOVE 0 TO CODEGEN-FILE-OFFSET

    CALL "SystemTimeMicros" USING CODEGEN-START-TIME

    DISPLAY "Codegen started: " FUNCTION TRIM(CODEGEN-FILE-NAME)

    GOBACK.

END PROGRAM Codegen-Start.

*> --- Codegen-Append ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-Append.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.
    01 WRITE-LENGTH                 PIC X(4) USAGE COMP-X.
LINKAGE SECTION.
    01 LK-BUFFER                    PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-BUFFER.
    MOVE FUNCTION STORED-CHAR-LENGTH(LK-BUFFER) TO WRITE-LENGTH

    CALL "CBL_WRITE_FILE" USING CODEGEN-FILE-HANDLE CODEGEN-FILE-OFFSET WRITE-LENGTH 0 LK-BUFFER
    COPY ASSERT REPLACING COND BY ==RETURN-CODE = 0==,
        MSG BY =="Codegen: Failed to write to file: " FUNCTION TRIM(CODEGEN-FILE-NAME)==.

    ADD WRITE-LENGTH TO CODEGEN-FILE-OFFSET

    GOBACK.

END PROGRAM Codegen-Append.

*> --- Codegen-End ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-End.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.
    01 CODEGEN-START-TIME           BINARY-LONG-LONG            EXTERNAL.
    01 CODEGEN-END-TIME             BINARY-LONG-LONG.
    01 CODEGEN-DURATION             BINARY-LONG-LONG.
    01 DISPLAY-INT                  PIC -(9)9.

PROCEDURE DIVISION.
    CALL "CBL_CLOSE_FILE" USING CODEGEN-FILE-HANDLE
    COPY ASSERT REPLACING COND BY ==RETURN-CODE = 0==,
        MSG BY =="Codegen: Failed to close file: " FUNCTION TRIM(CODEGEN-FILE-NAME)==.

    CALL "SystemTimeMicros" USING CODEGEN-END-TIME
    COMPUTE CODEGEN-DURATION = (CODEGEN-END-TIME - CODEGEN-START-TIME) / 1000
    MOVE CODEGEN-DURATION TO DISPLAY-INT

    DISPLAY "Codegen finished: " FUNCTION TRIM(CODEGEN-FILE-NAME) " in " FUNCTION TRIM(DISPLAY-INT) "ms"

    GOBACK.

END PROGRAM Codegen-End.
