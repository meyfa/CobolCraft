IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SpawnEntity.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "play/clientbound/minecraft:add_entity".
    *> buffer used to store the packet data
    01 PAYLOAD                  PIC X(1024).
    01 PAYLOADPOS               BINARY-LONG UNSIGNED.
    01 PAYLOADLEN               BINARY-LONG UNSIGNED.
    01 TEMP-INT16               BINARY-SHORT.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-ENTITY-ID             BINARY-LONG.
    01 LK-ENTITY-UUID           PIC X(16).
    01 LK-ENTITY-TYPE           BINARY-LONG.
    01 LK-POSITION.
        02 LK-X                 FLOAT-LONG.
        02 LK-Y                 FLOAT-LONG.
        02 LK-Z                 FLOAT-LONG.
    01 LK-ROTATION.
        02 LK-YAW               FLOAT-SHORT.
        02 LK-PITCH             FLOAT-SHORT.
    01 LK-VELOCITY.
        02 LK-VELOCITY-X        FLOAT-LONG.
        02 LK-VELOCITY-Y        FLOAT-LONG.
        02 LK-VELOCITY-Z        FLOAT-LONG.

PROCEDURE DIVISION USING LK-CLIENT LK-ENTITY-ID LK-ENTITY-UUID LK-ENTITY-TYPE LK-POSITION LK-ROTATION LK-VELOCITY.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    CALL "Encode-VarInt" USING LK-ENTITY-ID PAYLOAD PAYLOADPOS

    MOVE LK-ENTITY-UUID(1:16) TO PAYLOAD(PAYLOADPOS:16)
    ADD 16 TO PAYLOADPOS

    CALL "Encode-VarInt" USING LK-ENTITY-TYPE PAYLOAD PAYLOADPOS

    CALL "Encode-Double" USING LK-X PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING LK-Y PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING LK-Z PAYLOAD PAYLOADPOS

    CALL "Encode-Angle" USING LK-YAW PAYLOAD PAYLOADPOS
    CALL "Encode-Angle" USING LK-PITCH PAYLOAD PAYLOADPOS

    *> TODO: implement as a separate field
    CALL "Encode-Angle" USING LK-YAW PAYLOAD PAYLOADPOS

    *> data (?)
    MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
    ADD 1 TO PAYLOADPOS

    *> Velocity is in units of 1/8000 of a block per tick
    COMPUTE TEMP-INT16 = LK-VELOCITY-X / 8000
    CALL "Encode-Short" USING TEMP-INT16 PAYLOAD PAYLOADPOS
    COMPUTE TEMP-INT16 = LK-VELOCITY-Y / 8000
    CALL "Encode-Short" USING TEMP-INT16 PAYLOAD PAYLOADPOS
    COMPUTE TEMP-INT16 = LK-VELOCITY-Z / 8000
    CALL "Encode-Short" USING TEMP-INT16 PAYLOAD PAYLOADPOS

    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-SpawnEntity.
