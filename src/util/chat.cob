*> --- SendChatMessage ---
*> Send a chat message to a specific client. A client ID of 0 means to write the message to the server console.
IDENTIFICATION DIVISION.
PROGRAM-ID. SendChatMessage.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-CLIENTS.
    01 BYTE-COUNT               BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT-ID             BINARY-LONG UNSIGNED.
    01 LK-MESSAGE               PIC X ANY LENGTH.
    01 LK-COLOR                 PIC X ANY LENGTH.
    01 LK-MESSAGE-LENGTH        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT-ID LK-MESSAGE OPTIONAL LK-COLOR OPTIONAL LK-MESSAGE-LENGTH.
    IF LK-MESSAGE-LENGTH IS OMITTED
       MOVE FUNCTION STORED-CHAR-LENGTH(LK-MESSAGE) TO BYTE-COUNT
    ELSE
       MOVE LK-MESSAGE-LENGTH TO BYTE-COUNT
    END-IF

    IF LK-CLIENT-ID = 0 *> console
        DISPLAY LK-MESSAGE(1:BYTE-COUNT)
        GOBACK
    END-IF

    IF LK-COLOR IS OMITTED OR LK-COLOR = SPACES
        CALL "SendPacket-SystemChat" USING LK-CLIENT-ID LK-MESSAGE BYTE-COUNT "white"
    ELSE
        CALL "SendPacket-SystemChat" USING LK-CLIENT-ID LK-MESSAGE BYTE-COUNT LK-COLOR
    END-IF

    GOBACK.

END PROGRAM SendChatMessage.

*> --- BroadcastChatMessage ---
*> Send a chat message to all clients in the "play" state, and log it to the server console.
IDENTIFICATION DIVISION.
PROGRAM-ID. BroadcastChatMessage.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CLIENT-ID                BINARY-LONG UNSIGNED        VALUE 0.
LINKAGE SECTION.
    01 LK-MESSAGE               PIC X ANY LENGTH.
    01 LK-MESSAGE-LENGTH        BINARY-LONG UNSIGNED.
    01 LK-COLOR                 PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-MESSAGE LK-MESSAGE-LENGTH OPTIONAL LK-COLOR.
    IF LK-COLOR IS OMITTED
        CALL "BroadcastChatMessageExcept" USING CLIENT-ID LK-MESSAGE LK-MESSAGE-LENGTH OMITTED
    ELSE
        CALL "BroadcastChatMessageExcept" USING CLIENT-ID LK-MESSAGE LK-MESSAGE-LENGTH LK-COLOR
    END-IF
    GOBACK.

END PROGRAM BroadcastChatMessage.

*> --- BroadcastChatMessageExcept ---
*> Send a chat message to all clients in the "play" state, except for a specific client, and log the message to the
*> server console. A client ID of 0 means to broadcast to all clients.
IDENTIFICATION DIVISION.
PROGRAM-ID. BroadcastChatMessageExcept.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-CLIENT-STATES.
    COPY DD-CLIENTS.
    COPY DD-SERVER-PROPERTIES.
    01 MESSAGE-COLOR            PIC X(16).
    01 CLIENT-ID                BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-EXCEPT-CLIENT-ID      BINARY-LONG UNSIGNED.
    01 LK-MESSAGE               PIC X ANY LENGTH.
    01 LK-MESSAGE-LENGTH        BINARY-LONG UNSIGNED.
    01 LK-COLOR                 PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-EXCEPT-CLIENT-ID LK-MESSAGE LK-MESSAGE-LENGTH OPTIONAL LK-COLOR.
    DISPLAY LK-MESSAGE(1:LK-MESSAGE-LENGTH)

    IF LK-COLOR IS OMITTED
        MOVE "white" TO MESSAGE-COLOR
    ELSE
        MOVE LK-COLOR TO MESSAGE-COLOR
    END-IF

    PERFORM VARYING CLIENT-ID FROM 1 BY 1 UNTIL CLIENT-ID > MAX-CLIENTS
        IF CLIENT-STATE(CLIENT-ID) = CLIENT-STATE-PLAY AND CLIENT-ID NOT = LK-EXCEPT-CLIENT-ID
            CALL "SendPacket-SystemChat" USING CLIENT-ID LK-MESSAGE LK-MESSAGE-LENGTH MESSAGE-COLOR
        END-IF
    END-PERFORM

    GOBACK.

END PROGRAM BroadcastChatMessageExcept.
