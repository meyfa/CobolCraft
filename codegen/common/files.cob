IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-Start.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.
LINKAGE SECTION.
    01 LK-DIRECTORY                 PIC X ANY LENGTH.
    01 LK-FILENAME                  PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-DIRECTORY LK-FILENAME.
    INITIALIZE CODEGEN-FILE-NAME
    STRING FUNCTION TRIM(LK-DIRECTORY) "/" FUNCTION TRIM(LK-FILENAME) INTO CODEGEN-FILE-NAME

    CALL "CBL_CREATE_FILE" USING CODEGEN-FILE-NAME 2 0 0 CODEGEN-FILE-HANDLE
    IF RETURN-CODE NOT = 0
        DISPLAY "Codegen: Failed to open file: " FUNCTION TRIM(CODEGEN-FILE-NAME) UPON STDERR
        STOP RUN RETURNING 1
    END-IF

    MOVE 0 TO CODEGEN-FILE-OFFSET

    DISPLAY "Codegen started: " FUNCTION TRIM(CODEGEN-FILE-NAME)

    GOBACK.

END PROGRAM Codegen-Start.

IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-Append.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.
    01 WRITE-LENGTH                 PIC X(4) USAGE COMP-X.
LINKAGE SECTION.
    01 LK-BUFFER                    PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-BUFFER.
    MOVE FUNCTION STORED-CHAR-LENGTH(LK-BUFFER) TO WRITE-LENGTH

    CALL "CBL_WRITE_FILE" USING CODEGEN-FILE-HANDLE CODEGEN-FILE-OFFSET WRITE-LENGTH 0 LK-BUFFER
    IF RETURN-CODE NOT = 0
        DISPLAY "Codegen: Failed to write to file: " FUNCTION TRIM(CODEGEN-FILE-NAME) UPON STDERR
        STOP RUN RETURNING 1
    END-IF

    ADD WRITE-LENGTH TO CODEGEN-FILE-OFFSET

    GOBACK.

END PROGRAM Codegen-Append.

IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-End.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-FILE EXTERNAL.
        02 CODEGEN-FILE-NAME        PIC X(1024).
        02 CODEGEN-FILE-HANDLE      PIC X(4) USAGE COMP-X.
        02 CODEGEN-FILE-OFFSET      PIC X(8) USAGE COMP-X.

PROCEDURE DIVISION.
    CALL "CBL_CLOSE_FILE" USING CODEGEN-FILE-HANDLE
    IF RETURN-CODE NOT = 0
        DISPLAY "Codegen: Failed to close file: " FUNCTION TRIM(CODEGEN-FILE-NAME) UPON STDERR
        STOP RUN RETURNING 1
    END-IF

    DISPLAY "Codegen finished: " FUNCTION TRIM(CODEGEN-FILE-NAME)

    GOBACK.

END PROGRAM Codegen-End.
