IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-LoginSuccess.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE 2.
    *> temporary data used during encoding
    01 BUFFER           PIC X(64).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-HNDL          PIC X(4).
    01 LK-ERRNO         PIC 9(3).
    01 LK-PLAYER-ID     BINARY-LONG UNSIGNED.
    01 LK-USERNAME      PIC X(16).
    01 LK-USERNAME-LEN  BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING BY REFERENCE LK-HNDL LK-ERRNO LK-PLAYER-ID LK-USERNAME LK-USERNAME-LEN.
    MOVE 0 TO PAYLOADLEN

    *> player UUID
    *> TODO: encode the UUID in a better way
    MOVE X"000000000000000000000000000000" TO PAYLOAD(PAYLOADLEN + 1:15)
    ADD 15 TO PAYLOADLEN
    MOVE FUNCTION CHAR(LK-PLAYER-ID + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    *> username prefixed with VarInt length
    CALL "Encode-VarInt" USING LK-USERNAME-LEN BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN
    MOVE LK-USERNAME TO PAYLOAD(PAYLOADLEN + 1:LK-USERNAME-LEN)
    ADD LK-USERNAME-LEN TO PAYLOADLEN

    *> properties count=0
    MOVE X"00" TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    *> properties (omitted)

    *> Send the packet
    CALL "SendPacket" USING LK-HNDL PACKET-ID PAYLOAD PAYLOADLEN LK-ERRNO
    GOBACK.

END PROGRAM SendPacket-LoginSuccess.
