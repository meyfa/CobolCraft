*> --- Packets-Parse ---
*> Parse the packets.json file to get packet metadata.
IDENTIFICATION DIVISION.
PROGRAM-ID. Packets-Parse.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKETS.
    01 STATE-NAME               PIC X(128).
    01 DIRECTION-NAME           PIC X(128).
    01 PACKET-NAME              PIC X(128).
    01 PACKET-REFERENCE         PIC X(128).
    01 PROTOCOL-ID              BINARY-LONG.
LOCAL-STORAGE SECTION.
    01 OFFSET                   BINARY-LONG UNSIGNED        VALUE 1.
    01 EXIT-LOOP                BINARY-CHAR UNSIGNED        VALUE 0.
LINKAGE SECTION.
    01 LK-JSON                  PIC X ANY LENGTH.
    01 LK-JSON-LEN              BINARY-LONG UNSIGNED.
    01 LK-FAILURE               BINARY-CHAR UNSIGNED.

PROCEDURE DIVISION USING LK-JSON LK-JSON-LEN LK-FAILURE.
    MOVE 0 TO PACKET-ID-COUNT
    CALL "JsonParse-ObjectStart" USING LK-JSON OFFSET LK-FAILURE
    IF LK-FAILURE NOT = 0
        GOBACK
    END-IF
    PERFORM UNTIL EXIT-LOOP NOT = 0
        CALL "JsonParse-ObjectKey" USING LK-JSON OFFSET LK-FAILURE STATE-NAME
        IF LK-FAILURE NOT = 0
            GOBACK
        END-IF
        PERFORM ParseState
        CALL "JsonParse-Comma" USING LK-JSON OFFSET EXIT-LOOP
    END-PERFORM
    CALL "JsonParse-ObjectEnd" USING LK-JSON OFFSET LK-FAILURE
    GOBACK.

ParseState SECTION.
    CALL "JsonParse-ObjectStart" USING LK-JSON OFFSET LK-FAILURE
    IF LK-FAILURE NOT = 0
        GOBACK
    END-IF
    PERFORM UNTIL EXIT-LOOP NOT = 0
        CALL "JsonParse-ObjectKey" USING LK-JSON OFFSET LK-FAILURE DIRECTION-NAME
        IF LK-FAILURE NOT = 0
            GOBACK
        END-IF
        PERFORM ParseDirection
        CALL "JsonParse-Comma" USING LK-JSON OFFSET EXIT-LOOP
    END-PERFORM
    CALL "JsonParse-ObjectEnd" USING LK-JSON OFFSET LK-FAILURE
    EXIT SECTION.

ParseDirection SECTION.
    CALL "JsonParse-ObjectStart" USING LK-JSON OFFSET LK-FAILURE
    IF LK-FAILURE NOT = 0
        GOBACK
    END-IF
    PERFORM UNTIL EXIT-LOOP NOT = 0
        CALL "JsonParse-ObjectKey" USING LK-JSON OFFSET LK-FAILURE PACKET-NAME
        IF LK-FAILURE NOT = 0
            GOBACK
        END-IF
        PERFORM ParsePacket
        CALL "JsonParse-Comma" USING LK-JSON OFFSET EXIT-LOOP
    END-PERFORM
    CALL "JsonParse-ObjectEnd" USING LK-JSON OFFSET LK-FAILURE
    EXIT SECTION.

ParsePacket SECTION.
    INITIALIZE PACKET-REFERENCE
    STRING FUNCTION TRIM(STATE-NAME) "/" FUNCTION TRIM(DIRECTION-NAME) "/" FUNCTION TRIM(PACKET-NAME) INTO PACKET-REFERENCE
    MOVE -1 TO PROTOCOL-ID
    CALL "JsonParse-ObjectStart" USING LK-JSON OFFSET LK-FAILURE
    IF LK-FAILURE NOT = 0
        GOBACK
    END-IF
    PERFORM UNTIL EXIT-LOOP NOT = 0
        CALL "JsonParse-ObjectKey" USING LK-JSON OFFSET LK-FAILURE PACKET-NAME
        IF LK-FAILURE NOT = 0
            GOBACK
        END-IF
        EVALUATE PACKET-NAME
            WHEN "protocol_id"
                CALL "JsonParse-Integer" USING LK-JSON OFFSET LK-FAILURE PROTOCOL-ID
            WHEN OTHER
                CALL "JsonParse-SkipValue" USING LK-JSON OFFSET LK-FAILURE
        END-EVALUATE
        IF LK-FAILURE NOT = 0
            GOBACK
        END-IF
        CALL "JsonParse-Comma" USING LK-JSON OFFSET EXIT-LOOP
    END-PERFORM
    CALL "JsonParse-ObjectEnd" USING LK-JSON OFFSET LK-FAILURE
    IF PROTOCOL-ID = -1
        DISPLAY "Missing protocol_id for packet '" FUNCTION TRIM(PACKET-REFERENCE) "'"
        MOVE 1 TO LK-FAILURE
        GOBACK
    END-IF
    ADD 1 TO PACKET-ID-COUNT
    MOVE PACKET-REFERENCE TO PACKET-ID-REFERENCE(PACKET-ID-COUNT)
    MOVE PROTOCOL-ID TO PACKET-ID-NUMBER(PACKET-ID-COUNT)
    EXIT SECTION.

END PROGRAM Packets-Parse.

*> --- SendPacket ---
*> Send a raw packet to the client.
IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-CLIENTS.
    01 NUM-BYTES                BINARY-LONG UNSIGNED.
    01 HEADER                   PIC X(10).
    01 HEADER-OFFSET            BINARY-LONG UNSIGNED.
    01 TOTAL-LENGTH             BINARY-LONG UNSIGNED.
    01 HNDL                     PIC X(4).
    01 ERRNO                    PIC 9(3).
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-PACKET-ID             BINARY-LONG.
    01 LK-PAYLOAD               PIC X ANY LENGTH.
    01 LK-PAYLOAD-LENGTH        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-PACKET-ID LK-PAYLOAD LK-PAYLOAD-LENGTH.
    *> Don't send packet if the client is already in an error state. It will be disconnected on the next tick.
    IF CLIENT-ERRNO-SEND(LK-CLIENT) NOT = 0
        EXIT PROGRAM
    END-IF
    MOVE CLIENT-HNDL(LK-CLIENT) TO HNDL

    *> Packet length = length of packet ID + length of payload
    CALL "Encode-GetVarIntLength" USING LK-PACKET-ID NUM-BYTES
    COMPUTE TOTAL-LENGTH = NUM-BYTES + LK-PAYLOAD-LENGTH

    *> Send header: payload length, packet ID
    MOVE 1 TO HEADER-OFFSET
    CALL "Encode-VarInt" USING TOTAL-LENGTH HEADER HEADER-OFFSET
    CALL "Encode-VarInt" USING LK-PACKET-ID HEADER HEADER-OFFSET
    COMPUTE NUM-BYTES = HEADER-OFFSET - 1
    CALL "SocketWrite" USING HNDL NUM-BYTES HEADER GIVING ERRNO
    PERFORM HandleError

    *> Send packet data
    CALL "SocketWrite" USING HNDL LK-PAYLOAD-LENGTH LK-PAYLOAD GIVING ERRNO
    PERFORM HandleError

    GOBACK.

HandleError SECTION.
    IF ERRNO NOT = 0
        *> Mark the client as errored
        MOVE ERRNO TO CLIENT-ERRNO-SEND(LK-CLIENT)
        EXIT PROGRAM
    END-IF

    EXIT SECTION.

END PROGRAM SendPacket.
