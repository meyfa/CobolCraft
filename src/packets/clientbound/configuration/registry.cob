IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-Registry.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "configuration/clientbound/minecraft:registry_data".
    *> buffer used to store the packet data
    01 PAYLOAD                  PIC X(60000).
    01 PAYLOADPOS               BINARY-LONG UNSIGNED.
    01 PAYLOADLEN               BINARY-LONG UNSIGNED.
    *> temporary data
    01 REGISTRY-NAME            PIC X(255).
    01 REGISTRY-ENTRY-COUNT     BINARY-LONG UNSIGNED.
    01 REGISTRY-ENTRY-ID        BINARY-LONG UNSIGNED.
    01 REGISTRY-ENTRY-NAME      PIC X(255).
    01 INT32                    BINARY-LONG.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-REGISTRY-ID           BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-REGISTRY-ID.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    *> registry name
    CALL "Registries-Name" USING LK-REGISTRY-ID REGISTRY-NAME
    MOVE FUNCTION STORED-CHAR-LENGTH(REGISTRY-NAME) TO INT32
    CALL "Encode-String" USING REGISTRY-NAME INT32 PAYLOAD PAYLOADPOS

    *> entry count
    CALL "Registries-EntryCount" USING LK-REGISTRY-ID REGISTRY-ENTRY-COUNT
    CALL "Encode-VarInt" USING REGISTRY-ENTRY-COUNT PAYLOAD PAYLOADPOS

    *> entries
    PERFORM VARYING REGISTRY-ENTRY-ID FROM 0 BY 1 UNTIL REGISTRY-ENTRY-ID >= REGISTRY-ENTRY-COUNT
        CALL "Registries-EntryName" USING LK-REGISTRY-ID REGISTRY-ENTRY-ID REGISTRY-ENTRY-NAME
        MOVE FUNCTION STORED-CHAR-LENGTH(REGISTRY-ENTRY-NAME) TO INT32
        CALL "Encode-String" USING REGISTRY-ENTRY-NAME INT32 PAYLOAD PAYLOADPOS

        *> has data (always false)
        MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
        ADD 1 TO PAYLOADPOS
    END-PERFORM

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-Registry.
