IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-LoginPlay.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'2B'.
    *> temporary data used during encoding
    01 INT32            BINARY-LONG.
    01 BUFFER           PIC X(64).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-ENTITY-ID     BINARY-LONG.
    01 LK-VIEW-DISTANCE BINARY-LONG.

PROCEDURE DIVISION USING LK-CLIENT LK-ENTITY-ID LK-VIEW-DISTANCE.
    MOVE 0 TO PAYLOADLEN

    *> entity ID
    CALL "Encode-Int" USING LK-ENTITY-ID BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> is hardcore=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> dimension count=1
    MOVE 1 TO INT32
    CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> dimension name array=["minecraft:overworld"]
    ADD 1 TO PAYLOADLEN
    MOVE FUNCTION CHAR(19 + 1) TO PAYLOAD(PAYLOADLEN:1)
    MOVE "minecraft:overworld" TO PAYLOAD(PAYLOADLEN + 1:19)
    ADD 19 TO PAYLOADLEN

    *> max players=1
    MOVE 10 TO INT32
    CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> view distance
    CALL "Encode-VarInt" USING LK-VIEW-DISTANCE BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> simulation distance = view distance
    CALL "Encode-VarInt" USING LK-VIEW-DISTANCE BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> reduced debug info=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> enable respawn screen=true
    ADD 1 TO PAYLOADLEN
    MOVE X"01" TO PAYLOAD(PAYLOADLEN:1)

    *> do limited crafting=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> dimension type (ID in the minecraft:dimension_type registry)
    *> TODO: get this from the registry
    MOVE 0 TO INT32
    CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> dimension name="minecraft:overworld"
    ADD 1 TO PAYLOADLEN
    MOVE FUNCTION CHAR(19 + 1) TO PAYLOAD(PAYLOADLEN:1)
    MOVE "minecraft:overworld" TO PAYLOAD(PAYLOADLEN + 1:19)
    ADD 19 TO PAYLOADLEN

    *> hashed seed=0 (8-byte long)
    MOVE X"0000000000000000" TO PAYLOAD(PAYLOADLEN + 1:8)
    ADD 8 TO PAYLOADLEN

    *> gamemode=1 (creative)
    ADD 1 TO PAYLOADLEN
    MOVE X"01" TO PAYLOAD(PAYLOADLEN:1)

    *> previous gamemode=-1
    ADD 1 TO PAYLOADLEN
    MOVE X"FF" TO PAYLOAD(PAYLOADLEN:1)

    *> is debug=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> is flat=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> has death location=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> portal cooldown=0
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> enforces secure chat=false
    ADD 1 TO PAYLOADLEN
    MOVE X"00" TO PAYLOAD(PAYLOADLEN:1)

    *> Send the packet
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-LoginPlay.
