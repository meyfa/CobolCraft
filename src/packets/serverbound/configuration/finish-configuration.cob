IDENTIFICATION DIVISION.
PROGRAM-ID. RecvPacket-FinishConfiguration.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-CLIENTS.
    COPY DD-CLIENT-STATES.
    COPY DD-PLAYERS.
    COPY DD-SERVER-PROPERTIES.
    01 PLAYER-ID                BINARY-LONG.
    01 WORLD-IS-HARDCORE        BINARY-CHAR UNSIGNED.
    01 BUFFER                   PIC X(255).
    01 BUFFERLEN                BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-BUFFER                PIC X ANY LENGTH.
    01 LK-OFFSET                BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-BUFFER LK-OFFSET.
    MOVE CLIENT-PLAYER(LK-CLIENT) TO PLAYER-ID

    IF CONFIG-FINISH(LK-CLIENT) = 0
        DISPLAY "[state=" CLIENT-STATE(LK-CLIENT) "] Client sent unexpected acknowledge finish configuration"
        CALL "Server-DisconnectClient" USING LK-CLIENT
        GOBACK
    END-IF

    MOVE CLIENT-STATE-PLAY TO CLIENT-STATE(LK-CLIENT)

    *> send "Login (play)" with player index as entity ID
    CALL "World-IsHardcore" USING WORLD-IS-HARDCORE
    CALL "SendPacket-LoginPlay" USING LK-CLIENT PLAYER-ID VIEW-DISTANCE PLAYER-GAMEMODE(PLAYER-ID) WORLD-IS-HARDCORE MAX-PLAYERS

    CALL "Server-SpawnPlayer" USING LK-CLIENT

    CALL "SendPacket-Commands" USING LK-CLIENT

    INITIALIZE BUFFER
    STRING FUNCTION TRIM(PLAYER-NAME(PLAYER-ID)) " joined the game" INTO BUFFER
    MOVE FUNCTION STORED-CHAR-LENGTH(BUFFER) TO BUFFERLEN
    CALL "BroadcastChatMessageExcept" USING LK-CLIENT BUFFER BUFFERLEN "yellow"

    GOBACK.

END PROGRAM RecvPacket-FinishConfiguration.
