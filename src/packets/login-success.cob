IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-LoginSuccess.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'02'.
    *> temporary data used during encoding
    01 BUFFER           PIC X(64).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-PLAYER-UUID   PIC X(16).
    01 LK-USERNAME      PIC X ANY LENGTH.
    01 LK-USERNAME-LEN  BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-PLAYER-UUID LK-USERNAME LK-USERNAME-LEN.
    MOVE 0 TO PAYLOADLEN

    *> player UUID
    MOVE LK-PLAYER-UUID(1:16) TO PAYLOAD(PAYLOADLEN + 1:16)
    ADD 16 TO PAYLOADLEN

    *> username prefixed with VarInt length
    CALL "Encode-VarInt" USING LK-USERNAME-LEN BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN
    MOVE LK-USERNAME TO PAYLOAD(PAYLOADLEN + 1:LK-USERNAME-LEN)
    ADD LK-USERNAME-LEN TO PAYLOADLEN

    *> properties count=0
    MOVE X"00" TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    *> properties (omitted)

    *> disconnect on invalid data
    MOVE X"01" TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    *> Send the packet
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-LoginSuccess.
