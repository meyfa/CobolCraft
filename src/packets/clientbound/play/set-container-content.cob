IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SetContainerContent.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "play/clientbound/minecraft:container_set_content".
    *> TODO ensure this is large enough (could overflow for large inventories)
    01 PAYLOAD                  PIC X(64000).
    01 PAYLOADPOS               BINARY-LONG UNSIGNED.
    01 PAYLOADLEN               BINARY-LONG UNSIGNED.
    01 SLOT-INDEX               BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-WINDOW-ID             BINARY-LONG.
    01 LK-WINDOW-STATE          BINARY-LONG.
    01 LK-INVENTORY-LENGTH      BINARY-LONG UNSIGNED.
    01 LK-INVENTORY.
        02 LK-SLOT OCCURS 0 TO 100 TIMES DEPENDING ON LK-INVENTORY-LENGTH.
            COPY DD-INVENTORY-SLOT REPLACING LEADING ==PREFIX== BY ==LK==.
    01 LK-MOUSE-SLOT.
        COPY DD-INVENTORY-SLOT REPLACING LEADING ==PREFIX== BY ==LK-MOUSE==.

PROCEDURE DIVISION USING LK-CLIENT LK-WINDOW-ID LK-WINDOW-STATE LK-INVENTORY-LENGTH LK-INVENTORY LK-MOUSE-SLOT.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    CALL "Encode-VarInt" USING LK-WINDOW-ID PAYLOAD PAYLOADPOS
    CALL "Encode-VarInt" USING LK-WINDOW-STATE PAYLOAD PAYLOADPOS
    CALL "Encode-VarInt" USING LK-INVENTORY-LENGTH PAYLOAD PAYLOADPOS

    PERFORM VARYING SLOT-INDEX FROM 1 BY 1 UNTIL SLOT-INDEX > LK-INVENTORY-LENGTH
        CALL "Encode-InventorySlot" USING LK-SLOT(SLOT-INDEX) PAYLOAD PAYLOADPOS
    END-PERFORM

    CALL "Encode-InventorySlot" USING LK-MOUSE-SLOT PAYLOAD PAYLOADPOS

    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-SetContainerContent.
