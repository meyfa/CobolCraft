*> This file contains common routines for entities. In the Java world, these would be found in parent classes and
*> invoked via super.method(). In COBOL, we call them explicitly.

*> --- EntityBase-Serialize ---
IDENTIFICATION DIVISION.
PROGRAM-ID. EntityBase-Serialize.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 LEN                    BINARY-LONG.
    01 TAG                    PIC X(16).
LINKAGE SECTION.
    COPY DD-CALLBACK-ENTITY-SERIALIZE.

PROCEDURE DIVISION USING LK-ENTITY LK-NBT-ENCODER-STATE LK-BUFFER.
    MOVE "UUID" TO TAG
    MOVE 4 TO LEN
    CALL "NbtEncode-UUID" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN LK-ENTITY-UUID

    MOVE "Pos" TO TAG
    MOVE 3 TO LEN
    CALL "NbtEncode-List" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-X
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-Y
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-Z
    CALL "NbtEncode-EndList" USING LK-NBT-ENCODER-STATE LK-BUFFER

    MOVE "Rotation" TO TAG
    MOVE 8 TO LEN
    CALL "NbtEncode-List" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN
    CALL "NbtEncode-Float" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-YAW
    CALL "NbtEncode-Float" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-PITCH
    CALL "NbtEncode-EndList" USING LK-NBT-ENCODER-STATE LK-BUFFER

    MOVE "Motion" TO TAG
    MOVE 6 TO LEN
    CALL "NbtEncode-List" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-VELOCITY-X
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-VELOCITY-Y
    CALL "NbtEncode-Double" USING LK-NBT-ENCODER-STATE LK-BUFFER OMITTED OMITTED LK-ENTITY-VELOCITY-Z
    CALL "NbtEncode-EndList" USING LK-NBT-ENCODER-STATE LK-BUFFER

    MOVE "OnGround" TO TAG
    MOVE 8 TO LEN
    CALL "NbtEncode-Byte" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN LK-ENTITY-ON-GROUND

    MOVE "NoGravity" TO TAG
    MOVE 9 TO LEN
    CALL "NbtEncode-Byte" USING LK-NBT-ENCODER-STATE LK-BUFFER TAG LEN LK-ENTITY-NO-GRAVITY

    *> TODO add more fields

    GOBACK.

END PROGRAM EntityBase-Serialize.

*> --- EntityBase-Deserialize ---
IDENTIFICATION DIVISION.
PROGRAM-ID. EntityBase-Deserialize.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 INT32                BINARY-LONG.
LINKAGE SECTION.
    COPY DD-CALLBACK-ENTITY-DESERIALIZE.

PROCEDURE DIVISION USING LK-ENTITY LK-NBT-DECODER-STATE LK-BUFFER LK-TAG.
    EVALUATE LK-TAG
        WHEN "UUID"
            CALL "NbtDecode-UUID" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-UUID
        WHEN "Pos"
            CALL "NbtDecode-List" USING LK-NBT-DECODER-STATE LK-BUFFER INT32
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-X
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-Y
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-Z
            CALL "NbtDecode-EndList" USING LK-NBT-DECODER-STATE LK-BUFFER
        WHEN "Rotation"
            CALL "NbtDecode-List" USING LK-NBT-DECODER-STATE LK-BUFFER INT32
            CALL "NbtDecode-Float" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-YAW
            CALL "NbtDecode-Float" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-PITCH
            CALL "NbtDecode-EndList" USING LK-NBT-DECODER-STATE LK-BUFFER
        WHEN "Motion"
            CALL "NbtDecode-List" USING LK-NBT-DECODER-STATE LK-BUFFER INT32
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-VELOCITY-X
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-VELOCITY-Y
            CALL "NbtDecode-Double" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-VELOCITY-Z
            CALL "NbtDecode-EndList" USING LK-NBT-DECODER-STATE LK-BUFFER
        WHEN "OnGround"
            CALL "NbtDecode-Byte" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-ON-GROUND
        WHEN "NoGravity"
            CALL "NbtDecode-Byte" USING LK-NBT-DECODER-STATE LK-BUFFER LK-ENTITY-NO-GRAVITY
        WHEN OTHER
            CALL "NbtDecode-Skip" USING LK-NBT-DECODER-STATE LK-BUFFER
    END-EVALUATE.

END PROGRAM EntityBase-Deserialize.
