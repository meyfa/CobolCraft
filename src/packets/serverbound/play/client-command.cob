IDENTIFICATION DIVISION.
PROGRAM-ID. RecvPacket-ClientCommand.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-CLIENTS.
    COPY DD-PLAYERS.
    01 PLAYER-ID                BINARY-LONG.
    *> payload
    01 COMMAND-ENUM             BINARY-LONG.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.
    01 LK-BUFFER                PIC X ANY LENGTH.
    01 LK-OFFSET                BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-BUFFER LK-OFFSET.
    MOVE CLIENT-PLAYER(LK-CLIENT) TO PLAYER-ID

    CALL "Decode-VarInt" USING LK-BUFFER LK-OFFSET COMMAND-ENUM

    *> command 0 = perform respawn
    IF COMMAND-ENUM NOT = 0
        GOBACK
    END-IF

    *> player must be dead to respawn
    IF PLAYER-HEALTH(PLAYER-ID) > 0
        GOBACK
    END-IF

    *> TODO deduplicate all of this with players.cob
    MOVE 20 TO PLAYER-HEALTH(PLAYER-ID)
    MOVE 20 TO PLAYER-FOOD-LEVEL(PLAYER-ID)
    MOVE 5 TO PLAYER-SATURATION(PLAYER-ID)
    MOVE 0 TO PLAYER-HURT-TIME(PLAYER-ID)
    CALL "World-FindSpawnLocation" USING PLAYER-POSITION(PLAYER-ID)
    INITIALIZE PLAYER-ROTATION(PLAYER-ID)
    INITIALIZE PLAYER-VELOCITY(PLAYER-ID)
    MOVE 1 TO PLAYER-ON-GROUND(PLAYER-ID)
    MOVE 0 TO PLAYER-AGAINST-WALL(PLAYER-ID)
    MOVE 0 TO PLAYER-FALL-DISTANCE(PLAYER-ID)
    MOVE 0 TO PLAYER-SNEAKING(PLAYER-ID)
    MOVE 0 TO PLAYER-FLYING(PLAYER-ID)
    MOVE 0 TO PLAYER-HOTBAR(PLAYER-ID)
    MOVE 0 TO PLAYER-INVENTORY-STATE(PLAYER-ID)
    MOVE 0 TO PLAYER-WINDOW-ID(PLAYER-ID)
    MOVE -1 TO PLAYER-WINDOW-TYPE(PLAYER-ID)
    MOVE 0 TO PLAYER-WINDOW-STATE(PLAYER-ID)
    MOVE -1 TO PLAYER-BLOCK-BREAKING-STAGE(PLAYER-ID)
    INITIALIZE PLAYER-UPDATE-SIGN-POSITION(PLAYER-ID)

    CALL "SendPacket-Respawn" USING LK-CLIENT PLAYER-GAMEMODE(PLAYER-ID)

    CALL "Server-SpawnPlayer" USING LK-CLIENT

    GOBACK.

END PROGRAM RecvPacket-ClientCommand.
