IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-BlockEntityData.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "play/clientbound/minecraft:block_entity_data".
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADPOS       BINARY-LONG UNSIGNED.
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
    *> NBT encoding
    COPY DD-CALLBACKS.
    COPY DD-NBT-ENCODER.
    01 SERIALIZE-PTR    PROGRAM-POINTER.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-POSITION.
        02 LK-X         BINARY-LONG.
        02 LK-Y         BINARY-LONG.
        02 LK-Z         BINARY-LONG.
    01 LK-BLOCK-ENTITY.
        COPY DD-BLOCK-ENTITY REPLACING LEADING ==BLOCK-ENTITY== BY ==LK-BLOCK-ENTITY==.

PROCEDURE DIVISION USING LK-CLIENT LK-POSITION LK-BLOCK-ENTITY.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    *> location
    CALL "Encode-Position" USING LK-POSITION PAYLOAD PAYLOADPOS

    *> block entity type
    CALL "Encode-VarInt" USING LK-BLOCK-ENTITY-ID PAYLOAD PAYLOADPOS

    *> NBT data
    SET SERIALIZE-PTR TO CB-PTR-BLOCK-ENTITY-SERIALIZE(LK-BLOCK-ENTITY-ID + 1)
    INITIALIZE NBTENC
    MOVE PAYLOADPOS TO NBTENC-OFFSET
    CALL "NbtEncode-Compound" USING NBTENC PAYLOAD OMITTED
    IF SERIALIZE-PTR NOT = NULL
        CALL SERIALIZE-PTR USING LK-BLOCK-ENTITY NBTENC PAYLOAD
    END-IF
    CALL "NbtEncode-EndCompound" USING NBTENC PAYLOAD
    MOVE NBTENC-OFFSET TO PAYLOADPOS

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-BlockEntityData.
