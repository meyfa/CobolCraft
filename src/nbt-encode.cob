*> --- NbtEncode-WriteString ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-WriteString.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 UINT16           BINARY-SHORT UNSIGNED.
    01 TEMP-BUFFER      PIC X(2).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-STRING        PIC X ANY LENGTH.
    01 LK-STRING-LENGTH BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET LK-STRING LK-STRING-LENGTH.
    *> string length
    MOVE LK-STRING-LENGTH TO UINT16
    CALL "Encode-UnsignedShort" USING UINT16 TEMP-BUFFER TEMP-BUFFER-LEN
    MOVE TEMP-BUFFER(1:TEMP-BUFFER-LEN) TO LK-BUFFER(LK-OFFSET:TEMP-BUFFER-LEN)
    ADD TEMP-BUFFER-LEN TO LK-OFFSET
    *> string value
    MOVE LK-STRING(1:LK-STRING-LENGTH) TO LK-BUFFER(LK-OFFSET:LK-STRING-LENGTH)
    ADD LK-STRING-LENGTH TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-WriteString.

*> --- NbtEncode-EndCompound ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-EndCompound.

DATA DIVISION.
LINKAGE SECTION.
    COPY DD-NBT-ENCODER REPLACING LEADING ==NBT-ENCODER== BY ==LK==.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-STATE LK-BUFFER LK-OFFSET.
    IF LK-LEVEL < 1 OR LK-STACK-TYPE(LK-LEVEL) NOT = X"0A"
        DISPLAY "ERROR: Missing compound start tag."
        STOP RUN
    END-IF
    MOVE X"00" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-EndCompound.

*> --- NbtEncode-Long ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-Long.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 TEMP-BUFFER      PIC X(8).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    COPY DD-NBT-ENCODER REPLACING LEADING ==NBT-ENCODER== BY ==LK==.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-NAME          PIC X ANY LENGTH.
    01 LK-NAME-LEN      BINARY-LONG UNSIGNED.
    01 LK-VALUE         BINARY-LONG-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-STATE LK-BUFFER LK-OFFSET OPTIONAL LK-NAME OPTIONAL LK-NAME-LEN LK-VALUE.
    *> long tag
    MOVE X"04" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    *> name
    IF LK-NAME IS NOT OMITTED AND LK-NAME-LEN IS NOT OMITTED
        CALL "NbtEncode-WriteString" USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN
    END-IF
    *> long value
    CALL "Encode-UnsignedLong" USING LK-VALUE TEMP-BUFFER TEMP-BUFFER-LEN
    MOVE TEMP-BUFFER(1:TEMP-BUFFER-LEN) TO LK-BUFFER(LK-OFFSET:TEMP-BUFFER-LEN)
    ADD TEMP-BUFFER-LEN TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-Long.

*> --- NbtEncode-Compound ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-Compound.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 UINT16           BINARY-SHORT UNSIGNED.
    01 TEMP-BUFFER      PIC X(2).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    COPY DD-NBT-ENCODER REPLACING LEADING ==NBT-ENCODER== BY ==LK==.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-NAME          PIC X ANY LENGTH.
    01 LK-NAME-LEN      BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-STATE LK-BUFFER LK-OFFSET OPTIONAL LK-NAME OPTIONAL LK-NAME-LEN.
    *> compound tag
    MOVE X"0A" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    *> name
    IF LK-NAME IS NOT OMITTED AND LK-NAME-LEN IS NOT OMITTED
        CALL "NbtEncode-WriteString" USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN
    END-IF
    *> Push the compound onto the stack.
    ADD 1 TO LK-LEVEL
    MOVE X"0A" TO LK-STACK-TYPE(LK-LEVEL)
    GOBACK.

END PROGRAM NbtEncode-Compound.

*> --- NbtEncode-RootCompound ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-RootCompound.

DATA DIVISION.
LINKAGE SECTION.
    COPY DD-NBT-ENCODER REPLACING LEADING ==NBT-ENCODER== BY ==LK==.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-STATE LK-BUFFER LK-OFFSET.
    IF LK-LEVEL > 0
        DISPLAY "ERROR: Root compound must be at level 0."
        STOP RUN
    END-IF
    *> The root compound is a special case of the named compound.
    *> It wraps all save data on disk. However, it isn't used on the network.
    MOVE X"0A" TO LK-BUFFER(LK-OFFSET:1)
    MOVE X"00" TO LK-BUFFER(LK-OFFSET + 1:1)
    MOVE X"00" TO LK-BUFFER(LK-OFFSET + 2:1)
    ADD 3 TO LK-OFFSET
    *> Push the root compound onto the stack.
    ADD 1 TO LK-LEVEL
    MOVE X"0A" TO LK-STACK-TYPE(LK-LEVEL)
    GOBACK.

END PROGRAM NbtEncode-RootCompound.
