IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-KnownPacks.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'0E'.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(1024).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
    *> temporary
    01 INT32            BINARY-LONG.
    01 BUFFER           PIC X(8).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-HNDL          PIC X(4).
    01 LK-ERRNO         PIC 9(3).

PROCEDURE DIVISION USING LK-HNDL LK-ERRNO.
    MOVE 0 TO PAYLOADLEN

    *> count = 1
    MOVE 1 TO INT32
    CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
    MOVE BUFFER TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> pack namespace: minecraft
    MOVE FUNCTION CHAR(9 + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN
    MOVE "minecraft" TO PAYLOAD(PAYLOADLEN + 1:9)
    ADD 9 TO PAYLOADLEN

    *> pack id: core
    MOVE FUNCTION CHAR(4 + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN
    MOVE "core" TO PAYLOAD(PAYLOADLEN + 1:4)
    ADD 4 TO PAYLOADLEN

    *> pack version: 1.20.5
    MOVE FUNCTION CHAR(6 + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN
    MOVE "1.20.5" TO PAYLOAD(PAYLOADLEN + 1:6)
    ADD 6 TO PAYLOADLEN

    *> send packet
    CALL "SendPacket" USING LK-HNDL PACKET-ID PAYLOAD PAYLOADLEN LK-ERRNO
    GOBACK.

END PROGRAM SendPacket-KnownPacks.
