IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-WorldEvent.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'28'.
    *> temporary data used during encoding
    01 BUFFER           PIC X(8).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(32).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-EVENT         BINARY-LONG.
    01 LK-POSITION.
        02 LK-X             BINARY-LONG.
        02 LK-Y             BINARY-LONG.
        02 LK-Z             BINARY-LONG.
    01 LK-DATA          BINARY-LONG.

PROCEDURE DIVISION USING LK-CLIENT LK-EVENT LK-POSITION LK-DATA.
    MOVE 0 TO PAYLOADLEN

    *> event type
    CALL "Encode-Int" USING LK-EVENT BUFFER BUFFERLEN
    MOVE BUFFER TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> position
    CALL "Encode-Position" USING LK-POSITION BUFFER BUFFERLEN
    MOVE BUFFER TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> data
    CALL "Encode-Int" USING LK-DATA BUFFER BUFFERLEN
    MOVE BUFFER TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> disable relative volume = false
    *> TODO set to true for some sounds, see: https://wiki.vg/Protocol#World_Event
    MOVE X"00" TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    *> send packet
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-WorldEvent.
