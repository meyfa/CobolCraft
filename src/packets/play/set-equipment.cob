IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SetEquipment.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "play/clientbound/minecraft:set_equipment".
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADPOS       BINARY-LONG UNSIGNED.
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-ENTITY-ID     BINARY-LONG.
    01 LK-MAINHAND.
        COPY DD-INVENTORY-SLOT REPLACING LEADING ==PREFIX== BY ==LK-MAINHAND==.

PROCEDURE DIVISION USING LK-CLIENT LK-ENTITY-ID LK-MAINHAND.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    *> entity ID
    CALL "Encode-VarInt" USING LK-ENTITY-ID PAYLOAD PAYLOADPOS

    *> equipment slot: 0=main hand
    MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
    ADD 1 TO PAYLOADPOS

    IF LK-MAINHAND-SLOT-ID < 0 OR LK-MAINHAND-SLOT-COUNT = 0
        *> count=0
        MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
        ADD 1 TO PAYLOADPOS
    ELSE
        *> count
        CALL "Encode-Byte" USING LK-MAINHAND-SLOT-COUNT PAYLOAD PAYLOADPOS

        *> item ID
        CALL "Encode-VarInt" USING LK-MAINHAND-SLOT-ID PAYLOAD PAYLOADPOS

        *> NBT data
        MOVE LK-MAINHAND-SLOT-NBT-DATA(1:LK-MAINHAND-SLOT-NBT-LENGTH) TO PAYLOAD(PAYLOADPOS:LK-MAINHAND-SLOT-NBT-LENGTH)
        ADD LK-MAINHAND-SLOT-NBT-LENGTH TO PAYLOADPOS
    END-IF

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-SetEquipment.
