IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SetEquipment.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'60'.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADPOS       BINARY-LONG UNSIGNED.
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-ENTITY-ID     BINARY-LONG.
    01 LK-MAINHAND.
        02 LK-MAINHAND-ID         BINARY-LONG.
        02 LK-MAINHAND-COUNT      BINARY-CHAR UNSIGNED.
        02 LK-MAINHAND-NBT-LENGTH BINARY-SHORT UNSIGNED.
        02 LK-MAINHAND-NBT-DATA   PIC X(1024).

PROCEDURE DIVISION USING LK-CLIENT LK-ENTITY-ID LK-MAINHAND.
    MOVE 1 TO PAYLOADPOS

    *> entity ID
    CALL "Encode-VarInt" USING LK-ENTITY-ID PAYLOAD PAYLOADPOS

    *> equipment slot: 0=main hand
    MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
    ADD 1 TO PAYLOADPOS

    IF LK-MAINHAND-ID < 0 OR LK-MAINHAND-COUNT = 0
        *> count=0
        MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
        ADD 1 TO PAYLOADPOS
    ELSE
        *> count
        CALL "Encode-Byte" USING LK-MAINHAND-COUNT PAYLOAD PAYLOADPOS

        *> item ID
        CALL "Encode-VarInt" USING LK-MAINHAND-ID PAYLOAD PAYLOADPOS

        *> NBT data
        MOVE LK-MAINHAND-NBT-DATA(1:LK-MAINHAND-NBT-LENGTH) TO PAYLOAD(PAYLOADPOS:LK-MAINHAND-NBT-LENGTH)
        ADD LK-MAINHAND-NBT-LENGTH TO PAYLOADPOS
    END-IF

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-SetEquipment.
