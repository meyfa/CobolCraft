*> --- NbtEncode-WriteString ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-WriteString.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 UINT16           BINARY-SHORT UNSIGNED.
    01 TEMP-BUFFER      PIC X(2).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-STRING        PIC X ANY LENGTH.
    01 LK-STRING-LENGTH BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET LK-STRING LK-STRING-LENGTH.
    *> string length
    MOVE LK-STRING-LENGTH TO UINT16
    CALL "Encode-UnsignedShort" USING UINT16 TEMP-BUFFER TEMP-BUFFER-LEN
    MOVE TEMP-BUFFER(1:TEMP-BUFFER-LEN) TO LK-BUFFER(LK-OFFSET:TEMP-BUFFER-LEN)
    ADD TEMP-BUFFER-LEN TO LK-OFFSET
    *> string value
    MOVE LK-STRING(1:LK-STRING-LENGTH) TO LK-BUFFER(LK-OFFSET:LK-STRING-LENGTH)
    ADD LK-STRING-LENGTH TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-WriteString.

*> --- NbtEncode-End ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-End.

DATA DIVISION.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET.
    MOVE X"00" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-End.

*> --- NbtEncode-Long ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-Long.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 TEMP-BUFFER      PIC X(8).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-VALUE         BINARY-LONG-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET LK-VALUE.
    *> long tag
    MOVE X"04" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    *> long value
    CALL "Encode-UnsignedLong" USING LK-VALUE TEMP-BUFFER TEMP-BUFFER-LEN
    MOVE TEMP-BUFFER(1:TEMP-BUFFER-LEN) TO LK-BUFFER(LK-OFFSET:TEMP-BUFFER-LEN)
    ADD TEMP-BUFFER-LEN TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-Long.

*> --- NbtEncode-NamedLong ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-NamedLong.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 UINT16           BINARY-SHORT UNSIGNED.
    01 TEMP-BUFFER      PIC X(8).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET    BINARY-LONG UNSIGNED.
    01 LK-NAME          PIC X ANY LENGTH.
    01 LK-NAME-LEN      BINARY-LONG UNSIGNED.
    01 LK-VALUE         BINARY-LONG-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN LK-VALUE.
    *> long tag
    MOVE X"04" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    *> name
    CALL "NbtEncode-WriteString" USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN
    *> long value
    CALL "Encode-UnsignedLong" USING LK-VALUE TEMP-BUFFER TEMP-BUFFER-LEN
    MOVE TEMP-BUFFER(1:TEMP-BUFFER-LEN) TO LK-BUFFER(LK-OFFSET:TEMP-BUFFER-LEN)
    ADD TEMP-BUFFER-LEN TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-NamedLong.

*> --- NbtEncode-NamedCompound ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-NamedCompound.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 UINT16           BINARY-SHORT UNSIGNED.
    01 TEMP-BUFFER      PIC X(2).
    01 TEMP-BUFFER-LEN  BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.
    01 LK-NAME          PIC X ANY LENGTH.
    01 LK-NAME-LEN      BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN.
    *> compound tag
    MOVE X"0A" TO LK-BUFFER(LK-OFFSET:1)
    ADD 1 TO LK-OFFSET
    *> name
    CALL "NbtEncode-WriteString" USING LK-BUFFER LK-OFFSET LK-NAME LK-NAME-LEN
    GOBACK.

END PROGRAM NbtEncode-NamedCompound.

*> --- NbtEncode-RootCompound ---
IDENTIFICATION DIVISION.
PROGRAM-ID. NbtEncode-RootCompound.

DATA DIVISION.
LINKAGE SECTION.
    01 LK-BUFFER        PIC X ANY LENGTH.
    01 LK-OFFSET        BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-BUFFER LK-OFFSET.
    *> The root compound is a special case of the named compound.
    *> It wraps all save data on disk. However, it isn't used on the network.
    MOVE X"0A" TO LK-BUFFER(LK-OFFSET:1)
    MOVE X"00" TO LK-BUFFER(LK-OFFSET + 1:1)
    MOVE X"00" TO LK-BUFFER(LK-OFFSET + 2:1)
    ADD 3 TO LK-OFFSET
    GOBACK.

END PROGRAM NbtEncode-RootCompound.
