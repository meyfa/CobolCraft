IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SetEquipment.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE 89.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
    *> temporary data
    01 BUFFER           PIC X(8).
    01 BUFFERLEN        BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-HNDL          PIC X(4).
    01 LK-ERRNO         PIC 9(3).
    01 LK-ENTITY-ID     BINARY-LONG.
    01 LK-MAINHAND.
        02 LK-MAINHAND-ID         BINARY-LONG.
        02 LK-MAINHAND-COUNT      BINARY-CHAR UNSIGNED.
        02 LK-MAINHAND-NBT-LENGTH BINARY-SHORT UNSIGNED.
        02 LK-MAINHAND-NBT-DATA   PIC X(1024).

PROCEDURE DIVISION USING LK-HNDL LK-ERRNO LK-ENTITY-ID LK-MAINHAND.
    MOVE 0 TO PAYLOADLEN

    *> entity ID
    CALL "Encode-VarInt" USING LK-ENTITY-ID BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> equipment slot: 0=main hand
    MOVE X"00" TO PAYLOAD(PAYLOADLEN + 1:1)
    ADD 1 TO PAYLOADLEN

    IF LK-MAINHAND-ID < 0 OR LK-MAINHAND-COUNT = 0
        *> present=false
        MOVE X"00" TO PAYLOAD(PAYLOADLEN + 1:1)
        ADD 1 TO PAYLOADLEN
    ELSE
        *> present=true
        MOVE X"01" TO PAYLOAD(PAYLOADLEN + 1:1)
        ADD 1 TO PAYLOADLEN

        *> item ID
        CALL "Encode-VarInt" USING LK-MAINHAND-ID BUFFER BUFFERLEN
        MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
        ADD BUFFERLEN TO PAYLOADLEN

        *> item count
        MOVE FUNCTION CHAR(LK-MAINHAND-COUNT + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
        ADD 1 TO PAYLOADLEN

        *> NBT data
        MOVE LK-MAINHAND-NBT-DATA(1:LK-MAINHAND-NBT-LENGTH) TO PAYLOAD(PAYLOADLEN + 1:LK-MAINHAND-NBT-LENGTH)
        ADD LK-MAINHAND-NBT-LENGTH TO PAYLOADLEN
    END-IF

    *> send packet
    CALL "SendPacket" USING LK-HNDL PACKET-ID PAYLOAD PAYLOADLEN LK-ERRNO
    GOBACK.

END PROGRAM SendPacket-SetEquipment.
