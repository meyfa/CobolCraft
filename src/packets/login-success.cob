IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-LoginSuccess.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'02'.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(64000).
    01 PAYLOADPOS       BINARY-LONG UNSIGNED.
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-PLAYER-UUID   PIC X(16).
    01 LK-USERNAME      PIC X ANY LENGTH.
    01 LK-USERNAME-LEN  BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT LK-PLAYER-UUID LK-USERNAME LK-USERNAME-LEN.
    MOVE 1 TO PAYLOADPOS

    *> player UUID
    MOVE LK-PLAYER-UUID(1:16) TO PAYLOAD(PAYLOADPOS:16)
    ADD 16 TO PAYLOADPOS

    *> username prefixed with VarInt length
    CALL "Encode-VarInt" USING LK-USERNAME-LEN PAYLOAD PAYLOADPOS
    MOVE LK-USERNAME TO PAYLOAD(PAYLOADPOS:LK-USERNAME-LEN)
    ADD LK-USERNAME-LEN TO PAYLOADPOS

    *> properties count=0
    MOVE X"00" TO PAYLOAD(PAYLOADPOS:1)
    ADD 1 TO PAYLOADPOS

    *> properties (omitted)

    *> disconnect on invalid data
    MOVE X"01" TO PAYLOAD(PAYLOADPOS:1)
    ADD 1 TO PAYLOADPOS

    *> Send the packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-LoginSuccess.
