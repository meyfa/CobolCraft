*> --- SendPacket-Commands ---
IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-Commands.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID                    BINARY-LONG             VALUE H'11'.
    *> buffer used to store the packet data
    01 PAYLOAD                      PIC X(160000).
    01 PAYLOADLEN                   BINARY-LONG UNSIGNED.
    *> temporary data
    01 BUFFER                       PIC X(8).
    01 BUFFERLEN                    BINARY-LONG UNSIGNED.
    01 UINT8                        BINARY-CHAR UNSIGNED.
    01 INT32                        BINARY-LONG.
    01 NODE-INDEX                   INDEX.
    01 CHILD-INDEX                  INDEX.
LINKAGE SECTION.
    01 LK-CLIENT                    BINARY-LONG UNSIGNED.
    COPY DD-COMMAND-NODES REPLACING LEADING ==PREFIX== BY ==LK==.

PROCEDURE DIVISION USING LK-CLIENT LK-NODES.
    MOVE 0 TO PAYLOADLEN

    *> count
    CALL "Encode-VarInt" USING LK-NODE-COUNT BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> nodes
    PERFORM VARYING NODE-INDEX FROM 1 BY 1 UNTIL NODE-INDEX > LK-NODE-COUNT
        *> flags
        MOVE LK-NODE-TYPE(NODE-INDEX) TO UINT8
        COMPUTE UINT8 = UINT8 + LK-NODE-EXECUTABLE(NODE-INDEX) * 4
        COMPUTE UINT8 = UINT8 + LK-NODE-HAS-REDIRECT(NODE-INDEX) * 8
        COMPUTE UINT8 = UINT8 + LK-NODE-HAS-SUGGESTIONS-TYPE(NODE-INDEX) * 16
        MOVE FUNCTION CHAR(UINT8 + 1) TO PAYLOAD(PAYLOADLEN + 1:1)
        ADD 1 TO PAYLOADLEN

        *> child count
        CALL "Encode-VarInt" USING LK-NODE-CHILD-COUNT(NODE-INDEX) BUFFER BUFFERLEN
        MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
        ADD BUFFERLEN TO PAYLOADLEN

        *> children
        PERFORM VARYING CHILD-INDEX FROM 1 BY 1 UNTIL CHILD-INDEX > LK-NODE-CHILD-COUNT(NODE-INDEX)
            CALL "Encode-VarInt" USING LK-NODE-CHILD-INDEX(NODE-INDEX, CHILD-INDEX) BUFFER BUFFERLEN
            MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
            ADD BUFFERLEN TO PAYLOADLEN
        END-PERFORM

        *> redirect node
        IF LK-NODE-HAS-REDIRECT(NODE-INDEX) = 1
            CALL "Encode-VarInt" USING LK-NODE-REDIRECT-INDEX(NODE-INDEX) BUFFER BUFFERLEN
            MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
            ADD BUFFERLEN TO PAYLOADLEN
        END-IF

        *> name (only for "literal" and "argument" nodes, types 1 and 2)
        IF LK-NODE-TYPE(NODE-INDEX) = 1 OR LK-NODE-TYPE(NODE-INDEX) = 2
            MOVE FUNCTION STORED-CHAR-LENGTH(LK-NODE-NAME(NODE-INDEX)) TO INT32
            CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
            MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
            ADD BUFFERLEN TO PAYLOADLEN
            MOVE LK-NODE-NAME(NODE-INDEX)(1:INT32) TO PAYLOAD(PAYLOADLEN + 1:INT32)
            ADD INT32 TO PAYLOADLEN
        END-IF

        *> only for "argument" nodes:
        IF LK-NODE-TYPE(NODE-INDEX) = 2
            *> parser
            CALL "Encode-VarInt" USING LK-NODE-PARSER(NODE-INDEX) BUFFER BUFFERLEN
            MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
            ADD BUFFERLEN TO PAYLOADLEN

            *> properties
            MOVE LK-NODE-PROPERTIES-LENGTH(NODE-INDEX) TO INT32
            MOVE LK-NODE-PROPERTIES(NODE-INDEX)(1:INT32) TO PAYLOAD(PAYLOADLEN + 1:INT32)
            ADD INT32 TO PAYLOADLEN
        END-IF

        *> suggestions type
        IF LK-NODE-HAS-SUGGESTIONS-TYPE(NODE-INDEX) = 1
            MOVE FUNCTION STORED-CHAR-LENGTH(LK-NODE-SUGGESTIONS-TYPE(NODE-INDEX)) TO INT32
            CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
            MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
            ADD BUFFERLEN TO PAYLOADLEN
            MOVE LK-NODE-SUGGESTIONS-TYPE(NODE-INDEX)(1:INT32) TO PAYLOAD(PAYLOADLEN + 1:INT32)
            ADD INT32 TO PAYLOADLEN
        END-IF
    END-PERFORM

    *> root index (always 0)
    MOVE 0 TO INT32
    CALL "Encode-VarInt" USING INT32 BUFFER BUFFERLEN
    MOVE BUFFER(1:BUFFERLEN) TO PAYLOAD(PAYLOADLEN + 1:BUFFERLEN)
    ADD BUFFERLEN TO PAYLOADLEN

    *> send packet
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-Commands.
