*> --- Test: decode.cob ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Test-Decode.

PROCEDURE DIVISION.
    COPY TEST-SUITE REPLACING ==NAME== BY =="encoding/decode.cob"==.
    CALL "Test-Decode-Byte"
    CALL "Test-Decode-UnsignedShort"
    CALL "Test-Decode-Short"
    CALL "Test-Decode-UnsignedInt"
    CALL "Test-Decode-Int"
    CALL "Test-Decode-VarInt"
    CALL "Test-Decode-UnsignedLong"
    CALL "Test-Decode-Long"
    CALL "Test-Decode-Double"
    CALL "Test-Decode-Float"
    CALL "Test-Decode-String"
    CALL "Test-Decode-Position"
    CALL "Test-Decode-InventorySlot"
    GOBACK.

    *> --- Test: Decode-Byte ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Byte.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-CHAR.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Byte"==.
    Byte0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00"==.
        MOVE X"00" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Byte" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 2==.
    Byte127.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x7F"==.
        MOVE X"7F" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Byte" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 127 AND BUFFERPOS = 2==.
    ByteNegative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF"==.
        MOVE X"FF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Byte" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1 AND BUFFERPOS = 2==.
    ByteNegative128.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF"==.
        MOVE X"80" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Byte" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -128 AND BUFFERPOS = 2==.

        GOBACK.

    END PROGRAM Test-Decode-Byte.

    *> --- Test: Decode-UnsignedShort ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-UnsignedShort.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-SHORT UNSIGNED.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-UnsignedShort"==.
    Short0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00"==.
        MOVE X"0000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedShort" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 3==.
    Short1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x01 = 1"==.
        MOVE X"0001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedShort" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 3==.
    ShortMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0xFF = 65536"==.
        MOVE X"FFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedShort" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 65535 AND BUFFERPOS = 3==.

        GOBACK.

    END PROGRAM Test-Decode-UnsignedShort.

    *> --- Test: Decode-Short ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Short.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-SHORT.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Short"==.
    Short0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00"==.
        MOVE X"0000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Short" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 3==.
    Short1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x01 = 1"==.
        MOVE X"0001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Short" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 3==.
    ShortMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x7F 0xFF = 32767"==.
        MOVE X"7FFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Short" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 32767 AND BUFFERPOS = 3==.
    ShortMin.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x80 0x00 = -32768"==.
        MOVE X"8000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Short" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -32768 AND BUFFERPOS = 3==.

        GOBACK.

    END PROGRAM Test-Decode-Short.

    *> --- Test: Decode-UnsignedInt ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-UnsignedInt.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-LONG UNSIGNED.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-UnsignedInt"==.
    Int0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00 0x00 0x00"==.
        MOVE X"00000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 5==.
    Int1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00 0x00 0x01 = 1"==.
        MOVE X"00000001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 5==.
    IntMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0xFF 0xFF 0xFF = 4294967295"==.
        MOVE X"FFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 4294967295 AND BUFFERPOS = 5==.

        GOBACK.

    END PROGRAM Test-Decode-UnsignedInt.

    *> --- Test: Decode-Int ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Int.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-LONG.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Int"==.
    Int0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00 0x00 0x00"==.
        MOVE X"00000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Int" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 5==.
    Int1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00 0x00 0x00 0x01 = 1"==.
        MOVE X"00000001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Int" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 5==.
    IntMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x7F 0xFF 0xFF 0xFF = 2147483647"==.
        MOVE X"7FFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Int" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 2147483647 AND BUFFERPOS = 5==.
    Negative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0xFF 0xFF 0xFF = -1"==.
        MOVE X"FFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Int" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1 AND BUFFERPOS = 5==.
    NegativeMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x80 0x00 0x00 0x00 = -2147483648"==.
        MOVE X"80000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Int" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -2147483648 AND BUFFERPOS = 5==.

        GOBACK.

    END PROGRAM Test-Decode-Int.

    *> --- Test: Decode-VarInt ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-VarInt.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-LONG.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-VarInt"==.
    OneByte0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x00"==.
        MOVE X"00" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 2==.
    OneByte1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x01 = 1"==.
        MOVE X"01" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 2==.
    OneByte127.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x7F = 127"==.
        MOVE X"7F" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 127 AND BUFFERPOS = 2==.
    FiveByte.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0x80 0x80 0x80 0x00 = 127"==.
        MOVE X"FF80808000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 127 AND BUFFERPOS = 6==.
    SixByte.
        *> Note: Six bytes is illegal for a VarInt, so the last "continue" bit should be ignored.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0x80 0x80 0x80 0x80 0x00 = 127"==.
        MOVE X"FF8080808000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 127 AND BUFFERPOS = 6==.
    IntMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0xFF 0xFF 0xFF 0x07 = 2147483647"==.
        MOVE X"FFFFFFFF07" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 2147483647 AND BUFFERPOS = 6==.
    Negative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFF 0xFF 0xFF 0xFF 0x0F = -1"==.
        MOVE X"FFFFFFFF0F" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1 AND BUFFERPOS = 6==.
    Negative2.
        COPY TEST-CASE REPLACING ==NAME== BY =="0xFE 0xFF 0xFF 0xFF 0x0F = -2"==.
        MOVE X"FEFFFFFF0F" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -2 AND BUFFERPOS = 6==.
    NegativeMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="0x80 0x80 0x80 0x80 0x08 = -2147483648"==.
        MOVE X"8080808008" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-VarInt" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -2147483648 AND BUFFERPOS = 6==.

        GOBACK.

    END PROGRAM Test-Decode-VarInt.

    *> --- Test: Decode-UnsignedLong ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-UnsignedLong.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-LONG-LONG UNSIGNED.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-UnsignedLong"==.
    Long0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0"==.
        MOVE X"0000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedLong" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 9==.
    Long1.
        COPY TEST-CASE REPLACING ==NAME== BY =="1"==.
        MOVE X"0000000000000001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedLong" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 9==.
    PositiveMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="18446744073709551615"==.
        MOVE X"FFFFFFFFFFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-UnsignedLong" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 18446744073709551615 AND BUFFERPOS = 9==.

        GOBACK.

    END PROGRAM Test-Decode-UnsignedLong.

    *> --- Test: Decode-Long ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Long.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       BINARY-LONG-LONG.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Long"==.
    Long0.
        COPY TEST-CASE REPLACING ==NAME== BY =="0"==.
        MOVE X"0000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Long" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0 AND BUFFERPOS = 9==.
    Long1.
        COPY TEST-CASE REPLACING ==NAME== BY =="1"==.
        MOVE X"0000000000000001" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Long" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1 AND BUFFERPOS = 9==.
    PositiveMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="9223372036854775807"==.
        MOVE X"7FFFFFFFFFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Long" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 9223372036854775807 AND BUFFERPOS = 9==.
    Negative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="-1"==.
        MOVE X"FFFFFFFFFFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Long" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1 AND BUFFERPOS = 9==.
    NegativeMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="-9223372036854775808"==.
        MOVE X"8000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Long" USING BUFFER BUFFERPOS RESULT
        *> Unfortunately in this case, the C compiler doesn't like the literal -9223372036854775808.
        *> So instead, we add 1 to the result and compare it to (-9223372036854775808 + 1).
        ADD 1 TO RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -9223372036854775807 AND BUFFERPOS = 9==.

        GOBACK.

    END PROGRAM Test-Decode-Long.

    *> --- Test: Decode-Double ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Double.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       FLOAT-LONG.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Double"==.
    PositiveZero.
        COPY TEST-CASE REPLACING ==NAME== BY =="+0.0"==.
        MOVE X"0000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0.0 AND BUFFERPOS = 9==.
    NegativeZero.
        COPY TEST-CASE REPLACING ==NAME== BY =="-0.0"==.
        MOVE X"8000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -0.0 AND BUFFERPOS = 9==.
    Positive1.
        COPY TEST-CASE REPLACING ==NAME== BY =="+1.0"==.
        MOVE X"3FF0000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1.0 AND BUFFERPOS = 9==.
    Negative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="-1.0"==.
        MOVE X"BFF0000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1.0 AND BUFFERPOS = 9==.
    PositiveMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="+1.79769E+308"==.
        MOVE X"7FEFFFFFFFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1.79769313486231570814527423731704357E+308 AND BUFFERPOS = 9==.
    NegativeMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="-1.79769E+308"==.
        MOVE X"FFEFFFFFFFFFFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Double" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1.79769313486231570814527423731704357E+308 AND BUFFERPOS = 9==.

        GOBACK.

    END PROGRAM Test-Decode-Double.

    *> --- Test: Decode-Float ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Float.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT       FLOAT-SHORT.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Float"==.
    PositiveZero.
        COPY TEST-CASE REPLACING ==NAME== BY =="+0.0"==.
        MOVE X"00000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 0.0 AND BUFFERPOS = 5==.
    NegativeZero.
        COPY TEST-CASE REPLACING ==NAME== BY =="-0.0"==.
        MOVE X"80000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -0.0 AND BUFFERPOS = 5==.
    Positive1.
        COPY TEST-CASE REPLACING ==NAME== BY =="+1.0"==.
        MOVE X"3F800000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 1.0 AND BUFFERPOS = 5==.
    Negative1.
        COPY TEST-CASE REPLACING ==NAME== BY =="-1.0"==.
        MOVE X"BF800000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -1.0 AND BUFFERPOS = 5==.
    PositiveMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="+3.40282E+38"==.
        MOVE X"7F7FFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = 3.40282346638528859811704183484516925E+38 AND BUFFERPOS = 5==.
    NegativeMax.
        COPY TEST-CASE REPLACING ==NAME== BY =="-3.40282E+38"==.
        MOVE X"FF7FFFFF" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Float" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT = -3.40282346638528859811704183484516925E+38 AND BUFFERPOS = 5==.

        GOBACK.

    END PROGRAM Test-Decode-Float.

    *> --- Test: Decode-String ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-String.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(20).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 STR-LEN      BINARY-LONG UNSIGNED.
        01 STR          PIC X(20).

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-String"==.
    Empty.
        COPY TEST-CASE REPLACING ==NAME== BY =="''"==.
        MOVE X"0041414141414141" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-String" USING BUFFER BUFFERPOS STR-LEN STR
        COPY TEST-ASSERT REPLACING COND BY ==STR-LEN = 0 AND STR = SPACES AND BUFFERPOS = 2==.
    HelloWorld.
        COPY TEST-CASE REPLACING ==NAME== BY =="'Hello, World!'"==.
        MOVE X"00000D48656C6C6F2C20576F726C6421FFFFFFFF" TO BUFFER
        MOVE 3 TO BUFFERPOS
        CALL "Decode-String" USING BUFFER BUFFERPOS STR-LEN STR
        COPY TEST-ASSERT REPLACING COND BY ==STR-LEN = 13 AND STR = "Hello, World!" AND BUFFERPOS = 17==.

        GOBACK.

    END PROGRAM Test-Decode-String.

    *> --- Test: Decode-Position ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-Position.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(10).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT.
            02 RESULT-X     BINARY-LONG.
            02 RESULT-Y     BINARY-LONG.
            02 RESULT-Z     BINARY-LONG.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-Position"==.
    AllZero.
        COPY TEST-CASE REPLACING ==NAME== BY =="0 0 0"==.
        MOVE X"0000000000000000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Position" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT-X = 0 AND RESULT-Y = 0 AND RESULT-Z = 0 AND BUFFERPOS = 9==.
    WikiVgExample.
        COPY TEST-CASE REPLACING ==NAME== BY =="18357644 831 -20882616"==.
        MOVE X"4607632C15B4833F" TO BUFFER
        MOVE 1 TO BUFFERPOS
        CALL "Decode-Position" USING BUFFER BUFFERPOS RESULT
        COPY TEST-ASSERT REPLACING COND BY ==RESULT-X = 18357644 AND RESULT-Y = 831 AND RESULT-Z = -20882616 AND BUFFERPOS = 9==.

        GOBACK.

    END PROGRAM Test-Decode-Position.

    *> --- Test: Test-Decode-InventorySlot ---
    IDENTIFICATION DIVISION.
    PROGRAM-ID. Test-Decode-InventorySlot.

    DATA DIVISION.
    WORKING-STORAGE SECTION.
        01 BUFFER       PIC X(256).
        01 BUFFERPOS    BINARY-LONG UNSIGNED.
        01 RESULT-SLOT.
            COPY DD-INVENTORY-SLOT REPLACING LEADING ==PREFIX== BY ==RESULT==.

    PROCEDURE DIVISION.
        COPY TEST-UNIT REPLACING ==NAME== BY =="Decode-InventorySlot"==.
    Empty.
        COPY TEST-CASE REPLACING ==NAME== BY =="empty"==.
        MOVE X"00" TO BUFFER
        MOVE 1 TO BUFFERPOS
        INITIALIZE RESULT-SLOT
        CALL "Decode-InventorySlot" USING BUFFER BUFFERPOS RESULT-SLOT
        COPY TEST-ASSERT REPLACING COND BY ==BUFFERPOS = 2 AND RESULT-SLOT-COUNT = 0==.
    Simple.
        COPY TEST-CASE REPLACING ==NAME== BY =="simple"==.
        MOVE X"2ADDC7010000" TO BUFFER
        MOVE 1 TO BUFFERPOS
        INITIALIZE RESULT-SLOT
        CALL "Decode-InventorySlot" USING BUFFER BUFFERPOS RESULT-SLOT
        COPY TEST-ASSERT REPLACING COND BY ==BUFFERPOS = 7 AND RESULT-SLOT-COUNT = 42 AND RESULT-SLOT-ID = 25565 AND RESULT-SLOT-NBT-LENGTH = 2 AND RESULT-SLOT-NBT-DATA(1:2) = X"0000"==.

        GOBACK.

    END PROGRAM Test-Decode-InventorySlot.

END PROGRAM Test-Decode.
