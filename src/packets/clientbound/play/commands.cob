*> --- SendPacket-Commands ---
IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-Commands.

DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "play/clientbound/minecraft:commands".
    *> buffer used to store the packet data
    01 PAYLOAD                      PIC X(160000).
    01 PAYLOADPOS                   BINARY-LONG UNSIGNED.
    01 PAYLOADLEN                   BINARY-LONG UNSIGNED.
    *> shared data
    COPY DD-COMMAND-NODES.
    *> temporary data
    01 UINT8                        BINARY-CHAR UNSIGNED.
    01 INT32                        BINARY-LONG.
    01 NODE-INDEX                   INDEX.
    01 CHILD-INDEX                  INDEX.
LINKAGE SECTION.
    01 LK-CLIENT                    BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT.
    COPY PROC-PACKET-INIT.

    MOVE 1 TO PAYLOADPOS

    *> count
    CALL "Encode-VarInt" USING COMMAND-NODE-COUNT PAYLOAD PAYLOADPOS

    *> nodes
    PERFORM VARYING NODE-INDEX FROM 1 BY 1 UNTIL NODE-INDEX > COMMAND-NODE-COUNT
        *> flags
        MOVE COMMAND-NODE-TYPE(NODE-INDEX) TO UINT8
        COMPUTE UINT8 = UINT8 + COMMAND-NODE-EXECUTABLE(NODE-INDEX) * 4
        COMPUTE UINT8 = UINT8 + COMMAND-NODE-HAS-REDIRECT(NODE-INDEX) * 8
        COMPUTE UINT8 = UINT8 + COMMAND-NODE-HAS-SUGGESTIONS-TYPE(NODE-INDEX) * 16
        MOVE FUNCTION CHAR(UINT8 + 1) TO PAYLOAD(PAYLOADPOS:1)
        ADD 1 TO PAYLOADPOS

        *> child count
        CALL "Encode-VarInt" USING COMMAND-NODE-CHILD-COUNT(NODE-INDEX) PAYLOAD PAYLOADPOS

        *> children
        PERFORM VARYING CHILD-INDEX FROM 1 BY 1 UNTIL CHILD-INDEX > COMMAND-NODE-CHILD-COUNT(NODE-INDEX)
            CALL "Encode-VarInt" USING COMMAND-NODE-CHILD-INDEX(NODE-INDEX, CHILD-INDEX) PAYLOAD PAYLOADPOS
        END-PERFORM

        *> redirect node
        IF COMMAND-NODE-HAS-REDIRECT(NODE-INDEX) = 1
            CALL "Encode-VarInt" USING COMMAND-NODE-REDIRECT-INDEX(NODE-INDEX) PAYLOAD PAYLOADPOS
        END-IF

        *> name (only for "literal" and "argument" nodes, types 1 and 2)
        IF COMMAND-NODE-TYPE(NODE-INDEX) = 1 OR 2
            MOVE FUNCTION STORED-CHAR-LENGTH(COMMAND-NODE-NAME(NODE-INDEX)) TO INT32
            CALL "Encode-String" USING COMMAND-NODE-NAME(NODE-INDEX) INT32 PAYLOAD PAYLOADPOS
        END-IF

        *> only for "argument" nodes:
        IF COMMAND-NODE-TYPE(NODE-INDEX) = 2
            *> parser
            CALL "Encode-VarInt" USING COMMAND-NODE-PARSER(NODE-INDEX) PAYLOAD PAYLOADPOS

            *> properties
            MOVE COMMAND-NODE-PROPERTIES-LENGTH(NODE-INDEX) TO INT32
            MOVE COMMAND-NODE-PROPERTIES(NODE-INDEX)(1:INT32) TO PAYLOAD(PAYLOADPOS:INT32)
            ADD INT32 TO PAYLOADPOS
        END-IF

        *> suggestions type
        IF COMMAND-NODE-HAS-SUGGESTIONS-TYPE(NODE-INDEX) = 1
            MOVE FUNCTION STORED-CHAR-LENGTH(COMMAND-NODE-SUGGESTIONS-TYPE(NODE-INDEX)) TO INT32
            CALL "Encode-String" USING COMMAND-NODE-SUGGESTIONS-TYPE(NODE-INDEX) INT32 PAYLOAD PAYLOADPOS
        END-IF
    END-PERFORM

    *> root index (always 0)
    MOVE 0 TO INT32
    CALL "Encode-VarInt" USING INT32 PAYLOAD PAYLOADPOS

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-Commands.
