IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-SetPlayerPosition.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 PACKET-ID        BINARY-LONG             VALUE H'42'.
    *> buffer used to store the packet data
    01 PAYLOAD          PIC X(255).
    01 PAYLOADPOS       BINARY-LONG UNSIGNED.
    01 PAYLOADLEN       BINARY-LONG UNSIGNED.
    01 VELOCITY.
        02 VELOCITY-X  FLOAT-LONG               VALUE 0.
        02 VELOCITY-Y  FLOAT-LONG               VALUE 0.
        02 VELOCITY-Z  FLOAT-LONG               VALUE 0.
LINKAGE SECTION.
    01 LK-CLIENT        BINARY-LONG UNSIGNED.
    01 LK-POSITION.
        02 LK-X         FLOAT-LONG.
        02 LK-Y         FLOAT-LONG.
        02 LK-Z         FLOAT-LONG.
    01 LK-ROTATION.
        02 LK-YAW       FLOAT-SHORT.
        02 LK-PITCH     FLOAT-SHORT.
    01 LK-TELEPORT-ID   BINARY-LONG.

PROCEDURE DIVISION USING LK-CLIENT LK-POSITION LK-ROTATION LK-TELEPORT-ID.
    MOVE 1 TO PAYLOADPOS

    *> teleport ID
    CALL "Encode-VarInt" USING LK-TELEPORT-ID PAYLOAD PAYLOADPOS

    *> position
    CALL "Encode-Double" USING LK-X PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING LK-Y PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING LK-Z PAYLOAD PAYLOADPOS

    *> velocity
    *> TODO implement
    CALL "Encode-Double" USING VELOCITY-X PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING VELOCITY-Y PAYLOAD PAYLOADPOS
    CALL "Encode-Double" USING VELOCITY-Z PAYLOAD PAYLOADPOS

    *> rotation
    CALL "Encode-Float" USING LK-YAW PAYLOAD PAYLOADPOS
    CALL "Encode-Float" USING LK-PITCH PAYLOAD PAYLOADPOS

    *> flags (0 = all values are absolute)
    MOVE X"00000000" TO PAYLOAD(PAYLOADPOS:4)
    ADD 4 TO PAYLOADPOS

    *> send packet
    COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-SetPlayerPosition.
