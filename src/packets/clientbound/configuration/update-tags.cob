IDENTIFICATION DIVISION.
PROGRAM-ID. SendPacket-UpdateTags.


DATA DIVISION.
WORKING-STORAGE SECTION.
    COPY DD-PACKET REPLACING IDENTIFIER BY "configuration/clientbound/minecraft:update_tags".
    COPY DD-TAGS.
    *> buffer used to store the packet data
    01 PAYLOAD                  PIC X(64000).
    01 PAYLOADPOS               BINARY-LONG UNSIGNED.
    01 PAYLOADLEN               BINARY-LONG UNSIGNED.
    *> temporary data
    01 INT32                    BINARY-LONG.
    01 REGISTRY-INDEX           BINARY-LONG UNSIGNED.
    01 TAG-INDEX                BINARY-LONG UNSIGNED.
    01 ENTRY-INDEX              BINARY-LONG UNSIGNED.
LINKAGE SECTION.
    01 LK-CLIENT                BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-CLIENT.
    COPY PROC-PACKET-INIT.

    *> We assume that tags never change, so the packet data can be computed once and reused for every client.
    IF PAYLOADLEN = 0
        MOVE 1 TO PAYLOADPOS

        *> registries
        CALL "Encode-VarInt" USING TAGS-REGISTRY-COUNT PAYLOAD PAYLOADPOS
        PERFORM VARYING REGISTRY-INDEX FROM 1 BY 1 UNTIL REGISTRY-INDEX > TAGS-REGISTRY-COUNT
            *> registry identifier
            MOVE FUNCTION STORED-CHAR-LENGTH(TAGS-REGISTRY-NAME(REGISTRY-INDEX)) TO INT32
            CALL "Encode-String" USING TAGS-REGISTRY-NAME(REGISTRY-INDEX) INT32 PAYLOAD PAYLOADPOS

            *> tags
            CALL "Encode-VarInt" USING TAGS-REGISTRY-LENGTH(REGISTRY-INDEX) PAYLOAD PAYLOADPOS
            PERFORM VARYING TAG-INDEX FROM 1 BY 1 UNTIL TAG-INDEX > TAGS-REGISTRY-LENGTH(REGISTRY-INDEX)
                *> tag identifier
                MOVE FUNCTION STORED-CHAR-LENGTH(TAGS-REGISTRY-TAG-NAME(REGISTRY-INDEX, TAG-INDEX)) TO INT32
                CALL "Encode-String" USING TAGS-REGISTRY-TAG-NAME(REGISTRY-INDEX, TAG-INDEX) INT32 PAYLOAD PAYLOADPOS

                *> entries
                CALL "Encode-VarInt" USING TAGS-REGISTRY-TAG-LENGTH(REGISTRY-INDEX, TAG-INDEX) PAYLOAD PAYLOADPOS
                PERFORM VARYING ENTRY-INDEX FROM 1 BY 1 UNTIL ENTRY-INDEX > TAGS-REGISTRY-TAG-LENGTH(REGISTRY-INDEX, TAG-INDEX)
                    CALL "Encode-VarInt" USING TAGS-REGISTRY-TAG-ENTRY(REGISTRY-INDEX, TAG-INDEX, ENTRY-INDEX) PAYLOAD PAYLOADPOS
                END-PERFORM
            END-PERFORM
        END-PERFORM

        COMPUTE PAYLOADLEN = PAYLOADPOS - 1
    END-IF

    CALL "SendPacket" USING LK-CLIENT PACKET-ID PAYLOAD PAYLOADLEN
    GOBACK.

END PROGRAM SendPacket-UpdateTags.
