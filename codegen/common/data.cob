*> --- Codegen-SetDataDirectory ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-SetDataDirectory.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-DATA-DIR             PIC X(255)                  EXTERNAL.
LINKAGE SECTION.
    01 LK-DATA-DIR                  PIC X ANY LENGTH.

PROCEDURE DIVISION USING LK-DATA-DIR.
    MOVE LK-DATA-DIR TO CODEGEN-DATA-DIR
    GOBACK.

END PROGRAM Codegen-SetDataDirectory.

*> --- Codegen-ReadDataDirectory ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-ReadDataDirectory.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-DATA-DIR             PIC X(255)                  EXTERNAL.
    01 DIR-PATH                     PIC X(1024).
    01 DIR-PATH-LENGTH              BINARY-LONG UNSIGNED.
    01 DIR-HANDLE                   PIC X(8) USAGE COMP-X.
    01 FAILURE                      BINARY-CHAR UNSIGNED.
    01 DIR-ENTRY                    PIC X(255).
LINKAGE SECTION.
    01 LK-DIRNAME                   PIC X ANY LENGTH.
    COPY DD-CODEGEN-DIR-LIST REPLACING LEADING ==PREFIX== BY ==LK==.

PROCEDURE DIVISION USING LK-DIRNAME LK-DIR.
    INITIALIZE DIR-PATH
    STRING FUNCTION TRIM(CODEGEN-DATA-DIR) "/" FUNCTION TRIM(LK-DIRNAME) INTO DIR-PATH
    MOVE FUNCTION STORED-CHAR-LENGTH(DIR-PATH) TO DIR-PATH-LENGTH

    CALL "OpenDirectory" USING DIR-PATH DIR-PATH-LENGTH DIR-HANDLE GIVING FAILURE
    COPY ASSERT REPLACING COND BY ==FAILURE = 0==,
        MSG BY =="Codegen: Failed to open directory: " FUNCTION TRIM(DIR-PATH)==.

    MOVE 0 TO LK-DIR-SIZE

    PERFORM UNTIL EXIT
        CALL "ReadDirectory" USING DIR-HANDLE DIR-ENTRY GIVING FAILURE
        IF FAILURE NOT = 0
            EXIT PERFORM
        END-IF

        ADD 1 TO LK-DIR-SIZE
        MOVE DIR-ENTRY TO LK-DIR-NAME(LK-DIR-SIZE)
    END-PERFORM

    CALL "CloseDirectory" USING DIR-HANDLE

    GOBACK.

END PROGRAM Codegen-ReadDataDirectory.

*> --- Codegen-ReadDataFile ---
IDENTIFICATION DIVISION.
PROGRAM-ID. Codegen-ReadDataFile.

DATA DIVISION.
WORKING-STORAGE SECTION.
    01 CODEGEN-DATA-DIR             PIC X(255)                  EXTERNAL.
    01 FILE-PATH                    PIC X(1024).
    01 FILE-PATH-LENGTH             BINARY-LONG UNSIGNED.
    01 FAILURE                      BINARY-CHAR UNSIGNED.
LINKAGE SECTION.
    01 LK-FILENAME                  PIC X ANY LENGTH.
    01 LK-READ-BUFFER               PIC X ANY LENGTH.
    01 LK-READ-LENGTH               BINARY-LONG UNSIGNED.

PROCEDURE DIVISION USING LK-FILENAME LK-READ-BUFFER LK-READ-LENGTH.
    INITIALIZE FILE-PATH
    STRING FUNCTION TRIM(CODEGEN-DATA-DIR) "/" FUNCTION TRIM(LK-FILENAME) INTO FILE-PATH
    MOVE FUNCTION STORED-CHAR-LENGTH(FILE-PATH) TO FILE-PATH-LENGTH

    CALL "Files-ReadAll" USING FILE-PATH LK-READ-BUFFER LK-READ-LENGTH FAILURE
    COPY ASSERT REPLACING COND BY ==FAILURE = 0==,
        MSG BY =="Codegen: Failed to read: " FUNCTION TRIM(FILE-PATH)==.

    GOBACK.

END PROGRAM Codegen-ReadDataFile.
